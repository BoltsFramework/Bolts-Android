// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.1'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1x'
    }
}

allprojects {
    repositories {
        mavenCentral()
    }
}

ext {
    compileSdkVersion = 23
    buildToolsVersion = "23.0.1"

    minSdkVersion = 8
    targetSdkVersion = 23
}

task aggregateJavadocs(type: Javadoc) {
    destinationDir = file("$buildDir/docs/javadoc")
}

subprojects {
    afterEvaluate {
        if (plugins.hasPlugin(JavaPlugin)) {
            rootProject.tasks.aggregateJavadocs {
                source += javadoc.source
                classpath += files(javadoc.classpath)
            }
        }
        if (plugins.hasPlugin(com.android.build.gradle.LibraryPlugin)) {
            rootProject.tasks.aggregateJavadocs {
                source += files(android.sourceSets.main.java.srcDirs)
                classpath += files(project.files(android.getBootClasspath().join(File.pathSeparator)))
            }
        }
    }
}
