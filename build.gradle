// Copyright (c) Facebook, Inc. and its affiliates.
//
// This source code is licensed under the MIT license found in the/
// LICENSE file in the root directory of this source tree.

import com.android.build.gradle.LibraryPlugin

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.1'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1x'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
}

ext {
    compileSdkVersion = 28
    buildToolsVersion = "28.0.3"

    minSdkVersion = 14
    targetSdkVersion = 28
}

task aggregateJavadocs(type: Javadoc) {
    destinationDir = file("$buildDir/docs/javadoc")
}

subprojects {
    afterEvaluate {
        if (plugins.hasPlugin(JavaPlugin)) {
            rootProject.tasks.aggregateJavadocs {
                source += javadoc.source
                classpath += files(javadoc.classpath)
            }
        }
        if (plugins.hasPlugin(LibraryPlugin)) {
            rootProject.tasks.aggregateJavadocs {
                source += files(android.sourceSets.main.java.srcDirs)
                classpath += files(project.files(android.getBootClasspath().join(File.pathSeparator)))
            }
        }
    }
}
